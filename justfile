# Default project name
projectname := "aws-taggy"

# Enable dotenv loading
set dotenv-load

# Display help information ğŸ“–
default:
    @just --list

# List all available commands with descriptions ğŸ”
help:
    @just --list

# Build the Go binary ğŸ› ï¸
build: clean-build
    @echo "ğŸš€ Building AWS Taggy CLI..."
    @go mod tidy
    @cd cli && go build -ldflags "-X cmd.version=$(git describe --abbrev=0 --tags 2>/dev/null || echo devel)" -o ../{{projectname}}
    @echo "ğŸš€ AWS Taggy CLI built successfully!"

# Clean the build directory ğŸ§¹
clean-build:
    @echo "ğŸ§¹ Cleaning AWS Taggy CLI build directory..."
    @if [ -f "{{projectname}}" ]; then rm "{{projectname}}"; fi
    @echo "ğŸ§¹ AWS Taggy CLI compiled binary removed successfully!"

# Run the application directly ğŸš€ğŸ”§ Support arguments.
run *args:
    @echo "ğŸŒŸ Launching aws-taggy CLI in Developer Mode ğŸ–¥ï¸"
    @echo "ğŸ” Running from local source code..."
    @go run cli/main.go {{args}}

# Run the application directly ğŸš€ğŸ”§ Support arguments.
runbin *args: build
    @./{{projectname}} {{args}}

# Bootstrap development environment ğŸ”§
bootstrap:
    @go generate -tags tools tools/tools.go

# Run tests with coverage reporting ğŸ§ª
test: clean
    @go test --cover -parallel=1 -v -coverprofile=coverage.out ./...
    @go tool cover -func=coverage.out | sort -rnk3

# Clean up build artifacts and temporary files ğŸ§¹
clean: clean-build
    @echo "ğŸ§¹ Cleaning coverage.out, dist/ and compiled binary..."
    @rm -rf coverage.out dist/ {{projectname}}

# Generate detailed test coverage report ğŸ“Š
cover:
    @go test -v -race $(go list ./... | grep -v /vendor/) -v -coverprofile=coverage.out
    @go tool cover -func=coverage.out

# Format Go source code ğŸ–Œï¸
fmt:
    @echo "ğŸ“œ Formatting Go source code..."
    @echo "âœ… Formatting complete. Check formatted_files.log for details."
    @gofumpt -w .
    @go fmt ./...

# Run linters to ensure code quality ğŸ•µï¸
lint:
    @golangci-lint run --config=./.golangci.yml --timeout=5m --verbose

# Run pre-commit hooks for code quality checks ğŸ
run-hooks:
    @echo "Updating pre-commit hooks ğŸ§¼"
    @pre-commit autoupdate
    @pre-commit run --all-files

# Docker-related commands ğŸ³
# Build Docker image for Apple Silicon (arm64)
build-docker-arm:
    @docker buildx build \
        --platform linux/arm64 \
        -t aws-taggy:arm64 \
        --build-arg VERSION=$(git describe --abbrev=0 --tags 2>/dev/null || echo devel) \
        --load \
        .

# Build Docker image for Linux (amd64)
build-docker-linux:
    @docker buildx build \
        --platform linux/amd64 \
        -t aws-taggy:amd64 \
        --build-arg VERSION=$(git describe --abbrev=0 --tags 2>/dev/null || echo devel) \
        --load \
        .

# Build multi-platform Docker image
build-docker-multi:
    @docker buildx build \
        --platform linux/amd64,linux/arm64 \
        -t aws-taggy:latest \
        --build-arg VERSION=$(git describe --abbrev=0 --tags 2>/dev/null || echo devel) \
        --push \
        .

# Run Docker container for Apple Silicon
rundocker-arm *args:
    @just docker-build-arm
    @docker run --rm \
        -v "$(HOME)/.aws:/root/.aws" \
        -e AWS_PROFILE \
        -e AWS_DEFAULT_REGION \
        -e AWS_ACCESS_KEY_ID \
        -e AWS_SECRET_ACCESS_KEY \
        -e AWS_SESSION_TOKEN \
        aws-taggy:arm64 {{args}}

# Run Docker container for Linux
rundocker-linux *args:
    @just docker-build-linux
    @docker run --rm \
        -v "$(HOME)/.aws:/root/.aws" \
        -e AWS_PROFILE \
        -e AWS_DEFAULT_REGION \
        -e AWS_ACCESS_KEY_ID \
        -e AWS_SECRET_ACCESS_KEY \
        -e AWS_SESSION_TOKEN \
        aws-taggy:amd64 {{args}}

# Clean up Docker resources
docker-clean:
    @docker rmi aws-taggy:arm64 2>/dev/null || true
    @docker rmi aws-taggy:amd64 2>/dev/null || true
    @docker rmi aws-taggy:latest 2>/dev/null || true
    @docker system prune -f

# GitHub Actions-like Go Workflow ğŸ”
ci-go: fmt lint build
    @echo "ğŸ” Running Go CI (fmt, lint, build)"

# Comprehensive CI Check (Lint + Test) ğŸ
ci: ci-go test run-hooks
    @echo "âœ… All CI checks passed successfully!"

# Nix Development Shell ğŸŒ¿
# Commands for managing Nix development environment

# Start Nix development shell ğŸš€
nix-shell:
    @echo "ğŸŒ¿ Starting Nix Development Shell for AWS Taggy ğŸ·ï¸"
    @nix develop . --extra-experimental-features nix-command --extra-experimental-features flakes
